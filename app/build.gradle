plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.dokka'
}

def applicationName = "SakamichiApp"

android {
    compileSdk build_versions.target_sdk

    defaultConfig {
        applicationId "jp.mydns.kokoichi0206.sakamichiapp"
        minSdk build_versions.min_sdk
        targetSdk build_versions.target_sdk

        versionCode 1
        versionName "1.1.9"

        testApplicationId "jp.mydns.kokoichi0206.sakamichiapp.test"
        testInstrumentationRunner "jp.mydns.kokoichi0206.sakamichiapp.HiltTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    // Specify apk name
    applicationVariants.all { variant ->
        if (variant.buildType.name != "release") {
            return
        }
        variant.outputs.all {
            outputFileName = "${applicationName}-${versionName}.apk"
        }
    }

    signingConfigs {
        release {
            storeFile file('release.keystore') // 指定パスに配置
            storePassword System.getenv('KEYSTORE_PASSWORD') ?: RELEASE_STORE_PASSWORD
            keyAlias System.getenv('KEY_ALIAS') ?: RELEASE_KEY_ALIAS
            keyPassword System.getenv('KEY_PASSWORD') ?: RELEASE_KEY_PASSWORD
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            buildConfigField("String", "API_KEY", "\"${System.getenv('ENV_API_KEY')}\"")
        }
        debug {
            // Get API_KEY from gradle.properties
            buildConfigField("String", "API_KEY", API_KEY)
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion build_versions.compose
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    configurations.all {
        resolutionStrategy {
            force 'junit:junit:4.13.2'
        }
    }
    lint {
        baseline = file("lint-baseline.xml")
    }
}

dokkaGfm.configure {
//    dokkaHtml.configure {
    // projectDir は app フォルダのこと
    outputDirectory.set(new File("${projectDir}/build/dokkaGfm"))
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

dependencies {

    implementation deps.material.material
    implementation deps.material.compose

    implementation deps.core_ktx
    implementation deps.app_compat

    implementation deps.compose.ui
    implementation deps.compose.ui_preview
    debugImplementation deps.compose.debug_tooling

    implementation deps.compose.activity

    implementation deps.lifecycle.runtime
    implementation deps.test.runner
    implementation deps.navigation.testing
    testImplementation deps.test.junit

    // Compose dependencies
    implementation deps.compose.lifecycle_viewmodel
    implementation deps.compose.navigation
    implementation deps.accompanist.flowlayout

    // Retrofit
    implementation deps.retrofit.retrofit
    implementation deps.retrofit.gson
    implementation deps.retrofit.rxjava2
    implementation deps.retrofit.mock
    implementation deps.okhttp3.okhttp
    implementation deps.okhttp3.logging_interceptor
    // Room
    implementation deps.room.room
    kapt deps.room.kapt

    // Kotlin Extensions and Coroutines support for Room
    implementation deps.room.room_ktx

    // Timber
    implementation deps.timber

    // Coroutines
    implementation deps.coroutines.core
    implementation deps.coroutines.android

    // Coroutine Lifecycle Scopes
    implementation deps.lifecycle.viewmodel_ktx

    // Coil
    implementation deps.coil

    // Dagger - Hilt
    implementation deps.hilt.android
    kapt deps.hilt.compiler
    kapt deps.hilt.kapt
    implementation deps.hilt.navigation

    androidTestImplementation deps.hilt.testing
    kaptAndroidTest deps.hilt.kapt_android

    // Local Unit Tests
    implementation deps.test.core
    testImplementation deps.test.junit
    testImplementation deps.hamcrest
    testImplementation deps.arch.core
    testImplementation deps.coroutines.test
    testImplementation deps.truth
    testImplementation deps.okhttp3.mockwebserver
    testImplementation deps.mockk.mockk
    testImplementation deps.robolectric
    testImplementation deps.assertj

    // Instrumented Unit Tests
    androidTestImplementation deps.test.junit
    androidTestImplementation deps.coroutines.test
    androidTestImplementation deps.arch.core
    androidTestImplementation deps.truth
    androidTestImplementation deps.test.ext_junit
    androidTestImplementation deps.test.core_ktx
    androidTestImplementation deps.okhttp3.mockwebserver
    androidTestImplementation deps.mockk.android
    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.compose.ui_test_junit4

    // Action bar helper
    implementation deps.accompanist.systemuicontroller
    // Animated navigation
    implementation deps.accompanist.animation
    // Swipe to refresh
    implementation deps.accompanist.swiperefresh

    // DataStore
    implementation deps.datastore.preferences
    // optional - RxJava3 support
    implementation deps.datastore.rxjava3

    // Fix sqlite database error in m1 mac
    kapt deps.sqlite_jdbc

    // modules
    implementation project(":core:common")
    implementation project(":core:model")
    implementation project(":core:data")
    implementation project(":core:domain")
}